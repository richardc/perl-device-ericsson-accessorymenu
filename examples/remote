#!/usr/local/bin/perl -w
use lib qw(lib);
use strict;
use Device::Ericsson::AccessoryMenu;
use Device::SerialPort;
use Xmms::Remote;

my $remote = Device::Ericsson::AccessoryMenu->new;

my $port = Device::SerialPort->new( '/dev/rfcomm0' ) or die;
$remote->port( $port );

my $xmms = Xmms::Remote->new;

# start xmms if it isn't already
sub start_xmms {
    system ('xmms &') unless $xmms->is_running;
}
$remote->menu( [
    'Remote' => [
        XMMS => [
            "Play/Pause" => sub {
                start_xmms;
                $xmms->is_playing ? $xmms->pause : $xmms->play;
            },
            Back   => sub { $xmms->playlist_prev },
            Next   => sub { $xmms->playlist_next },
            Stop   => sub { $xmms->stop },
            Volume => sub {
                my $r = shift;
                $r->percent_slider(
                    title    => 'Volume',
                    value    => $xmms->get_main_volume,
                    steps    => 10,
                    callback => sub {
                        $xmms->set_main_volume( shift )
                    },
                   );
                return;
            },
            "Now Playing"  => sub {
                return "nothing" unless $xmms->is_playing;
                my $pos = $xmms->get_playlist_pos + 1;
                my ($rate, $freq, $nch) = $xmms->get_info;
                return join( '',
                             $pos, " of ", $xmms->get_playlist_length,
                             ". ", $xmms->get_playlist_titles->[ $pos - 1 ],
                             " ",  $xmms->get_output_timestr,
                            );
            },
           ],
        hello  => sub { my $r = shift; $r->send_text('hello, world', "isn't this fun"); return },
        time   => sub { scalar localtime },
        menu   => sub { [ foo => 'bar',] },
       ],
   ]);

$remote->register_menu;

print "Ready to rock.\n";

while (1) {
    $remote->control;
}


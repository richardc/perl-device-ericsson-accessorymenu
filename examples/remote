#!/usr/local/bin/perl -w
use lib qw(lib);
use strict;
use Device::Ericsson::AccessoryMenu;
use Device::SerialPort;
use Xmms::Remote;

my $remote = Device::Ericsson::AccessoryMenu->new;

my $port = Device::SerialPort->new( '/dev/rfcomm0' ) or die;
$remote->port( $port );

my $xmms = Xmms::Remote->new;
$remote->menu( [
    'Remote' => [
        XMMS => sub {
            # on enter, lauch if not launching already
            system ('xmms &') unless $xmms->is_running;
            my $volume = $xmms->get_main_volume;

            return [
                Play    => sub { $xmms->play;  return },
                Pause   => sub { $xmms->pause; return },
                Stop    => sub { $xmms->stop;  return },
                Louder  => sub {
                    $xmms->set_main_volume( $xmms->get_main_volume + 5);
                    return;
                },
                Quieter => sub {
                    $xmms->set_main_volume( $xmms->get_main_volume - 5);
                    return;
                },
               ];
        },
        hello  => sub { my $r = shift; $r->send_text('hello, world', "isn't this fun"); return },
        time   => sub { scalar localtime },
        menu   => sub { [ foo => 'bar',] },
       ],
   ]);

$remote->register_menu;

print "Ready to rock.\n";

while (1) {
    $remote->control;
}

